SET( GOFIGURE_APPLICATION_SOURCE_LIST
    gofigure.cxx
    )

INCLUDE_DIRECTORIES( ${GOFIGURE2_BINARY_DIR}/Main/  )

QT4_WRAP_UI( QGoMainWindow_UI_H ${QGoMainWindow_UI} )

QT4_WRAP_CPP( QGoGUISRC_MOC ${QGoGUISRC_HDRS} )

SET_SOURCE_FILES_PROPERTIES( ${QGoGUISRC_CXX} PROPERTIES
  OBJECT_DEPENDS "${QGoMainWindow_UI_H}"
  )

SET_SOURCE_FILES_PROPERTIES( ${QGoGUISRC_MOC} PROPERTIES
  OBJECT_DEPENDS "${QGoMainWindow_UI_H}"
  )

QT4_ADD_RESOURCES( QGoMainWindow_QRC
  ${QGoResourceFile}
)

SET( GUI_TYPE )

set(qt_menu_nib_sources)

IF( APPLE )

  IF(QT_MAC_USE_COCOA)
    GET_FILENAME_COMPONENT(qt_menu_nib
     "@QT_QTGUI_LIBRARY_RELEASE@/Resources/qt_menu.nib"
      REALPATH)
    set(qt_menu_nib_sources
      "${qt_menu_nib}/classes.nib"
      "${qt_menu_nib}/info.nib"
      "${qt_menu_nib}/keyedobjects.nib"
      )
    SET_SOURCE_FILES_PROPERTIES(
      ${qt_menu_nib_sources}
      PROPERTIES
      MACOSX_PACKAGE_LOCATION Resources/qt_menu.nib
      )
  ELSE(QT_MAC_USE_COCOA)
  ENDIF(QT_MAC_USE_COCOA)

  SET( GUI_TYPE MACOSX_BUNDLE )

  SET( MACOSX_BUNDLE_ICON_FILE
    ${GOFIGURE2_SOURCE_DIR}/Resources/myapp.icns
  )

  SET( GOFIGURE_APPLICATION_SOURCE_LIST
    ${GOFIGURE_APPLICATION_SOURCE_LIST}
    ${MACOSX_BUNDLE_ICON_FILE}
  )

SET( MACOSX_BUNDLE_SHORT_VERSION_STRING
    ${GOFIGURE2_VERSION}
    )

  SET( MACOSX_BUNDLE_VERSION
    ${GOFIGURE2_VERSION}
    )

  SET( MACOSX_BUNDLE_LONG_VERSION_STRING
    Version ${GOFIGURE2_VERSION}
    )

  SET_SOURCE_FILES_PROPERTIES( ${MACOSX_BUNDLE_ICON_FILE} PROPERTIES
    MACOSX_PACKAGE_LOCATION Resources
    )

ENDIF( APPLE )

IF( WIN32 )
  IF( MINGW )
    ADD_CUSTOM_COMMAND( OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/myapp.o
      COMMAND windres.exe -I${CMAKE_CURRENT_SOURCE_DIR}
        -i ${GOFIGURE2_SOURCE_DIR}/Code/GUI/resources/myapp.rc
        -o ${CMAKE_CURRENT_BINARY_DIR}/myapp.o
        )

    SET( ${GOFIGURE_APPLICATION_SOURCE_LIST}
      ${GOFIGURE_APPLICATION_SOURCE_LIST}
      ${CMAKE_CURRENT_BINARY_DIR}/myapp.o
      )
  ENDIF( MINGW )
ENDIF( WIN32 )

ADD_EXECUTABLE( gofigure
  ${GUI_TYPE}
  ${GOFIGURE_APPLICATION_SOURCE_LIST}
  ${QGoGUI_SRC}
  ${QGoGUISRC_MOC}
  ${QGoMainWindow_QRC}
  ${qt_menu_nib_sources} 
)

TARGET_LINK_LIBRARIES( gofigure
  QGoGui
  QGoIO
  ITKBasicFilters
  ITKNumerics
  ITKCommon
  ITKIO
  itkQt
 )

IF( APPLE )
  FUNCTION( MakeOSXBundleApp target binary_dir )
    SET( OSX_MAKE_STANDALONE_BUNDLE_CMAKE_SCRIPT 
      "${binary_dir}/${target}_OSX_MakeStandAloneBundle.cmake" )
    SET( OSX_MAKE_STANDALONE_BUNDLE_BASH_SCRIPT 
      "${binary_dir}/${target}_OSX_MakeStandAloneBundle.sh" )

    CONFIGURE_FILE( 
      "${GOFIGURE2_SOURCE_DIR}/CMake/OSX_Tools/CompleteBundle.cmake.in"
      "${OSX_MAKE_STANDALONE_BUNDLE_CMAKE_SCRIPT}" @ONLY IMMEDIATE
    )

    CONFIGURE_FILE(
      "${GOFIGURE2_SOURCE_DIR}/CMake/OSX_Tools/CreateBundle.sh.in"
      "${OSX_MAKE_STANDALONE_BUNDLE_BASH_SCRIPT}" @ONLY IMMEDIATE
    )

    INSTALL( SCRIPT "${OSX_MAKE_STANDALONE_BUNDLE_CMAKE_SCRIPT}" )
  ENDFUNCTION( MakeOSXBundleApp )

#  IF( CMAKE_BUILD_TYPE MATCHES "Debug" )
#    MakeOSXBundleApp( "gofigure_debug" ${GOFIGURE2_BINARY_DIR} )
#  ELSE( CMAKE_BUILD_TYPE MATCHES "Debug" )
    MakeOSXBundleApp( "gofigure" ${GOFIGURE2_BINARY_DIR} )
#  ENDIF( CMAKE_BUILD_TYPE MATCHES "Debug" )

  
  INSTALL( CODE "
    file(WRITE \"\${CMAKE_INSTALL_PREFIX}/gofigure.app/Contents/Resources/qt.conf\"\"\")
    " COMPONENT RUNTIME)

ELSE( APPLE )

  INCLUDE( ${GOFIGURE2_SOURCE_DIR}/CMake/InstallSupportLibraries.cmake )

  INSTALL( TARGETS gofigure
    ARCHIVE DESTINATION ${GOFIGURE2_LIB_INSTALL_DIR}
    LIBRARY DESTINATION ${GOFIGURE2_LIB_INSTALL_DIR}
    RUNTIME DESTINATION ${GOFIGURE2_BIN_INSTALL_DIR}
    BUNDLE  DESTINATION ${GOFIGURE2_BIN_INSTALL_DIR}
    COMPONENT applications
    )

ENDIF( APPLE )

